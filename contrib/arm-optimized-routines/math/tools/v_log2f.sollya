// polynomial used for __v_log2f(x)
//
// Copyright (c) 2022-2024, Arm Limited.
// SPDX-License-Identifier: MIT OR Apache-2.0 WITH LLVM-exception

deg = 9; // poly degree
a = -1/3;
b = 1/3;

ln2 = evaluate(log(2),0);
invln2 = single(1/ln2);

// find log2(1+x)/x polynomial with minimal relative error
// (minimal relative error polynomial for log2(1+x) is the same * x)
deg = deg-1; // because of /x

// f = log2(1+x)/x; using taylor series
f = 0;
for i from 0 to 60 do { f = f + (-x)^i/(i+1); };
f = f * invln2;

// return p that minimizes |f(x) - poly(x) - x^d*p(x)|/|f(x)|
approx = proc(poly,d) {
  return remez(1 - poly(x)/f(x), deg-d, [a;b], x^d/f(x), 1e-10);
};

// first coeff is fixed, iteratively find optimal double prec coeffs
poly = invln2;
for i from 1 to deg do {
  p = roundcoefficients(approx(poly,i), [|SG ...|]);
  poly = poly + x^i*coeff(p,0);
};

display = hexadecimal;
print("rel error:", accurateinfnorm(1-poly(x)/f(x), [a;b], 30));
print("in [",a,b,"]");
print("coeffs:");
for i from 0 to deg do coeff(poly,i);
