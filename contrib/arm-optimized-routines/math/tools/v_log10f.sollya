// polynomial for approximating v_log10f(1+x)
//
// Copyright (c) 2019-2024, Arm Limited.
// SPDX-License-Identifier: MIT OR Apache-2.0 WITH LLVM-exception

deg = 9; // poly degree
// |log10(1+x)| > 0x1p-4 outside the interval
a = -1/3;
b =  1/3;

display = hexadecimal;
print("log10(2) = ", single(log10(2)));

ln10 = evaluate(log(10),0);
invln10 = single(1/ln10);

// find log10(1+x)/x polynomial with minimal relative error
// (minimal relative error polynomial for log10(1+x) is the same * x)
deg = deg-1; // because of /x

// f = log(1+x)/x; using taylor series
f = 0;
for i from 0 to 60 do { f = f + (-x)^i/(i+1); };
f = f/ln10;

// return p that minimizes |f(x) - poly(x) - x^d*p(x)|/|f(x)|
approx = proc(poly,d) {
  return remez(1 - poly(x)/f(x), deg-d, [a;b], x^d/f(x), 1e-10);
};

// first coeff is fixed, iteratively find optimal double prec coeffs
poly = invln10;
for i from 1 to deg do {
  p = roundcoefficients(approx(poly,i), [|SG ...|]);
  poly = poly + x^i*coeff(p,0);
};
display = hexadecimal;
print("invln10:", invln10);
print("rel error:", accurateinfnorm(1-poly(x)/f(x), [a;b], 30));
print("in [",a,b,"]");
print("coeffs:");
for i from 0 to deg do single(coeff(poly,i));

display = decimal;
print("in [",a,b,"]");
