#!/usr/sbin/dtrace -s
/*-
 * Copyright 2014 John-Mark Gurney.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions
 * are met:
 * 1. Redistributions of source code must retain the above copyright
 *    notice, this list of conditions and the following disclaimer.
 * 2. Redistributions in binary form must reproduce the above copyright
 *    notice, this list of conditions and the following disclaimer in the
 *    documentation and/or other materials provided with the distribution.
 *
 * THIS SOFTWARE IS PROVIDED BY THE AUTHOR AND CONTRIBUTORS ``AS IS'' AND
 * ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED.  IN NO EVENT SHALL THE AUTHOR OR CONTRIBUTORS BE LIABLE
 * FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
 * DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS
 * OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION)
 * HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT
 * LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY
 * OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF
 * SUCH DAMAGE.
 *
 */

#pragma D option quiet
#pragma D option dynvarsize=16m

dtrace:::BEGIN
{
	printf("Tracing... Hit Ctrl-C to end.\n");
}

io:::start
{
	start_time[arg0] = timestamp;
}

io:::done
/this->start = start_time[arg0]/
{
	this->delta = (timestamp - this->start) / 1000;
	@q[args[1]->device_name, args[1]->unit_number] =
	    lquantize(this->delta, 4000, 80000, 4000);
	@max[args[1]->device_name, args[1]->unit_number] = max(this->delta);
	@avg[args[1]->device_name, args[1]->unit_number] = avg(this->delta);
	@stddev[args[1]->device_name, args[1]->unit_number] = stddev(this->delta);
	start_time[arg0] = 0;
}

tick-10s
{
	printa("   %s (%d), us:\n%@d\n", @q);
	printa("max%s (%d), us:\n%@d\n", @max);
	printa("avg%s (%d), us:\n%@d\n", @avg);
	printa("stddev%s (%d), us:\n%@d\n", @stddev);

	clear(@q);
	clear(@max);
	clear(@avg);
	clear(@stddev);
}
